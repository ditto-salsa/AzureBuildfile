SetChapterEvents(0x1, ThisChapter)

setText(0x160, NewChName)
ALIGN 4
NewChName:
String("Prologue: Lost and Found"); BYTE 0

ALIGN 4
ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN PlayerUnits PlayerUnits
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
SHORT 0x2 0; POIN ResetBlitz; BYTE 1 255 0 0
END_MAIN

ALIGN 32
CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
SHORT 0x8 0; POIN EmptyCellEvent; BYTE 8 1; SHORT 0x12
SHORT 0x8 0; POIN VathekCellEvent; BYTE 13 2; SHORT 0x12
SHORT 0x8 0; POIN EstherCellEvent; BYTE 5 9; SHORT 0x12
SHORT 0x8 0; POIN AinanCellEvent; BYTE 9 8; SHORT 0x12
SHORT 0x8 0; POIN JessCellEvent; BYTE 15 8; SHORT 0x12
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4
PlayerUnits:
UNIT Shino Thief Shino Level(14,Ally,0) [16,12] 0 0 0 0 [SteelSword,JusticeBlade,Vulnerary] NoAI
UNIT Lazarus SageF Shino Level(1,Ally,0) [16,13] 0 0 0 0 [Elfire, Apollo, Heal, 0] NoAI
UNIT

ALIGN 4
NPCUnits:
UNIT Jess Supplier Shino Level(9,NPC,0) [15,7] 0 0 0 0 NoItems NoAI
UNIT Esther General Shino Level(2,NPC,0) [5,7] 0 0 0 0 NoItems NoAI
UNIT Ainan ArcherF Shino Level(11,NPC,0) [10,6] 0 0 0 0 NoItems NoAI
UNIT Vathek MageM Shino Level(12,NPC,0) [13,1] 0 0 0 0 NoItems NoAI
UNIT


ALIGN 4
EnemyUnits:
UNIT Belana SwordmasterF Belana Level(20,Enemy,0) [1,1] 0 0 0 0 [BraveSword, Elixir] AttackInRangeAI //Boss

UNIT Cultist Myrmidon Belana Level(12,Enemy,1) [4,4] 0 0 0 0 [SteelSword] AttackInRangeAI

UNIT Cultist Sniper	Belana Level(1,Enemy,1) [13,3] DropItem 0 0 0 [EclipseBow,DoorKey] AttackInRangeAI

UNIT Cultist Armour Belana Level(11,Enemy,1) [12,6] 0 0 0 0 [IronLance] GuardTileAI
UNIT Cultist Armour Belana Level(11,Enemy,1) [13,6] 0 0 0 0 [SteelLance] GuardTileAI
UNIT Cultist Archer Belana Level(10,Enemy,1) [13,5] 0 0 0 0 [SteelBow] GuardTileAI
UNIT Cultist Archer Belana Level(10,Enemy,1) [12,5] 0 0 0 0 [IronBow] GuardTileAI

UNIT Cultist Fighter Belana Level(8,Enemy,1) [13,8] 0 0 0 0 [HandAxe,SteelAxe] AttackInRangeAI

UNIT


ALIGN 4
BeginningScene:
ENUT NeutralNPCSFlag
LOAD1 0x1 PlayerUnits; ENUN
LOAD1 0x1 EnemyUnits; ENUN
LOAD1 0x1 NPCUnits; ENUN
CALL SetBelanaStats
CHECK_TUTORIAL
BEQ 0 sC s0
	ASMC EasyModeStatModifier
LABEL 0
NoFade; ENDA

SetBelanaStats:
SetUnitStats(0x12, Belana, 67)
SVAL s1 0x13; ASMC SetUnitStats
SVAL s1 0x14; SVAL s3 27; ASMC SetUnitStats
SVAL s1 0x15; SVAL s3 30; ASMC SetUnitStats
SVAL s1 0x16; ASMC SetUnitStats
SVAL s1 0x17; SVAL s3 22; ASMC SetUnitStats
SVAL s1 0x19; ASMC SetUnitStats
SVAL s1 0x18; SVAL s3 5; ASMC SetUnitStats
SVAL s1 0x3A; SVAL s3 2; ASMC SetUnitStats
NoFade; ENDA

EndingScene:
ENUF NeutralNPCSFlag
MNC2 0x1
NoFade; ENDA

EmptyCellEvent:
TILECHANGE 0x0
Text(Prologue_EmptyCellText)
SVAL s3 BlightedBrew
GIVEITEMTO (-1)
NoFade; ENDA

VathekCellEvent:
TILECHANGE 0x1
MUSS MelliaSortie
Text(Prologue_VathekCellText)
CUSA Vathek
EventSetPopupUnit(Vathek)
ASMC GivenItemsWithSpace_PopupWrapper
SVAL s3 0
SVAL s2 Fire
ASMC SilentGiveItem
MURE 30
NoFade; ENDA

EstherCellEvent:
TILECHANGE 0x2
MUSS MelliaSortie
Text(Prologue_EstherCellText)
CUSA Esther
EventSetPopupUnit(Esther)
ASMC GivenItems_PopupWrapper
SVAL s3 0
SVAL s2 IronLance
ASMC SilentGiveItem
MURE 30
NoFade; ENDA

AinanCellEvent:
TILECHANGE 0x3
MUSS MelliaSortie
Text(Prologue_AinanCellText)
CUSA Ainan
EventSetPopupUnit(Ainan)
ASMC GivenItemsWithSpace_PopupWrapper
SVAL s3 0
SVAL s2 IronBow
ASMC SilentGiveItem
MURE 30
NoFade; ENDA

JessCellEvent:
TILECHANGE 0x4
MUSS MelliaSortie
Text(Prologue_JessCellText)
CUSA Jess
EventSetPopupUnit(Jess)
ASMC GivenItems_PopupWrapper
SVAL s3 0
SVAL s2 IronLance
ASMC SilentGiveItem
SVAL s2 JessticePike
ASMC SilentGiveItem
SVAL s2 Vulnerary
ASMC SilentGiveItem
MURE 30
NoFade; ENDA

PopupWrapper(GivenItems_PopupWrapper, GivenItems_PopupDefs, 120, 1, 0x240, 4)
GivenItems_PopupDefs:
Popup_SetSound(0x5A)
Popup_SetColour(0x2)
Popup_UnitName
Popup_SetColour(0x0)
Popup_StringPtr(Prologue_GivenXItems)
Popup_End

PopupWrapper(GivenItemsWithSpace_PopupWrapper, GivenItemsWithSpace_PopupDefs, 120, 1, 0x240, 4)
GivenItemsWithSpace_PopupDefs:
Popup_SetSound(0x5A)
Popup_SetColour(0x2)
Popup_UnitName
Popup_SetColour(0x0)
Popup_StringPtr(Prologue_GivenXItems)
Popup_Space(0x1)
Popup_End

Prologue_GivenXItems:
String(" has been given items!"); BYTE 0
